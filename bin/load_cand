#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use lib 'lib';
use TwittElection::Schema;

my $file = shift || 'db/candidate.tsv';

my $sch = TwittElection::Schema->connect(
  "dbi:mysql:database=$ENV{TE_DB}", $ENV{TE_USER}, $ENV{TE_PASS},
  { mysql_enable_utf8 => 1 },
) or die;

my $con_rs = $sch->resultset('Constituency');
my $par_rs = $sch->resultset('Party');

open my $fh, '<', $file or die "$!: $file";

#delete existing candidates
$sch->resultset('Candidate')->delete;

while (<$fh>) {
  chomp;

  my ($name, $twitter, $party, $constituency) = split /\t/;

  my $con = $con_rs->find({
    name => $constituency,
  });

  warn "Can't find constituency: $constituency", next unless $con;

  my $par = $par_rs->find({
    name => $party,
  });

  warn "Can't find party: $party", next unless $par;

  $con->add_to_candidates({
    name     => $name,
    twitter  => $twitter,
    party_id => $par->id,
  });
}
