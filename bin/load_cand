#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use lib 'lib';
use TwittElection::Schema;

my $file = shift || 'db/candidate.tsv';

my $sch = TwittElection::Schema->connect(
  "dbi:mysql:database=$ENV{TE_DB}", $ENV{TE_USER}, $ENV{TE_PASS},
  { mysql_enable_utf8 => 1 },
) or die;

my $con_rs = $sch->resultset('Constituency');
my $par_rs = $sch->resultset('Party');

open my $fh, '<:encoding(UTF-8)', $file or die "$!: $file";

#delete existing candidates
$sch->resultset('Candidate')->delete;

while (<$fh>) {
  chomp;

  my ($name, $yournextmp_id, $twitter, $party_id, $constituency_id)
    = split /\t/;

  my $con = $con_rs->find({
    mapit_id => $constituency_id,
  });

  warn "Can't find constituency: $constituency_id", next unless $con;

  my $par = $par_rs->find({
    yournextmp_id => $party_id,
  });

  warn "Can't find party: $party_id", next unless $par;

  $con->add_to_candidates({
    yournextmp_id => $yournextmp_id,
    name          => $name,
    twitter       => $twitter,
    party_id      => $par->id,
  });
}
