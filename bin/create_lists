#!/usr/bin/perl

use strict;
use warnings;
use 5.010;
use lib 'lib';

use TwittElection::Twitter;
use TwittElection::Schema;

my $sch = TwittElection::Schema->connect(
  "dbi:mysql:$ENV{TE_DB}", $ENV{TE_USER}, $ENV{TE_PASS},
) or die;

my $con_rs = $sch->resultset('Constituency');

my $t = TwittElection::Twitter->new(
  traits          => [ 'API::RESTv1_1', 'OAuth' ],
  ssl             => 1,
  consumer_key    => $ENV{TE_TW_API_KEY},
  consumer_secret => $ENV{TE_TW_API_SEC},
);

$t->authorise;

# Here's how this should work.
#
# IF constit has a list id and that list exists
#   Delete all members
# ELSE
#   Create new list
#   Update list name and list id
# END
#
# Insert list members

foreach my $con ($con_rs->all) {
  next unless $con->candidates->count;
  say $con->name;

  my $list;
  eval {
    if ($con->list_id) {
      $list = $t->show_list({
        owner_screen_name => 'twittelection',
        list_id           => $con->list_id,
      });
      if ($list) {
        foreach my $mem (@{$t->list_members({ list_id => $list->{id} })->{users}}) {
          $t->remove_list_members({
            list_id     => $list->{id},
            screen_name => $mem->{screen_name},
          });
        }
      } else {
        say 'Create ', $con->name;
        $list = $t->create_list({
          owner_screen_name => 'twittelection',
          name => $con->name,
        });

        say $list->{slug};

        $con->update({
          list_name => $list->{slug},
          list_id   => $list->{id},
        });
      }
    } else {
      say 'Create ', $con->name;
      $list = $t->create_list({
        owner_screen_name => 'twittelection',
        name => $con->name,
      });

      say $list->{slug};

      $con->update({
        list_name => $list->{slug},
        list_id   => $list->{id},
      });
    }
  };

  foreach my $cand ($con->candidates) {
    next unless $cand->twitter;
    say "$list->{id} -> ", $cand->name, ' / ', $cand->twitter;
    $t->add_list_member({
      list_id     => $list->{id},
      screen_name => $cand->twitter,
    });
  }
}
